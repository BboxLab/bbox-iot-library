apply plugin: 'com.android.library'
apply plugin: 'bintray-release'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        versionCode 1
        versionName "0.76"
        testApplicationId "fr.bouyguestelecom.tv.bboxiot.test"
        testInstrumentationRunner "fr.bouyguestelecom.tv.bboxiot.test.TestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    libraryVariants.all { variant ->

        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + "-" + getDateTime() + ".apk"))
        }

    }

    libraryVariants.all{
        variant ->
            task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
                description "Generates Javadoc for $variant.name."
                source = variant.javaCompile.source
                ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
                classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
                options.links("http://docs.oracle.com/javase/7/docs/api/");
                options.links("http://d.android.com/reference/");
                title = "bboxiot-lib " +defaultConfig.versionName;
            }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

repositories {
    maven { url 'http://repo1.maven.org/maven2' }
}

publish {
    userOrg = 'bboxlab'
    groupId = 'bboxlab'
    artifactId = 'bboxiot-library'
    publishVersion = '0.76'
    desc = 'BboxIoT Pre Release'
    website = 'https://github.com/BboxLab/bboxiot-library'
}

import java.text.DateFormat
import java.text.SimpleDateFormat

def getDateTime() {
    DateFormat df = new SimpleDateFormat("ddMMYYYY");

    return df.format(new Date());
}